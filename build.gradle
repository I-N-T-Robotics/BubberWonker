plugins {
    id 'java-library'
    id 'maven-publish'
    id "edu.wpi.first.GradleRIO" version "2024.2.1"
    id 'com.diffplug.spotless' version '6.25.0'
}

group 'com.github.I-N-T-Robotics'

repositories {
    mavenCentral()
}

dependencies {
    implementation wpi.java.deps.wpilib()
    implementation wpi.java.vendor.java()

    compileOnly 'org.projectlombok:lombok:1.18.30'
    annotationProcessor 'org.projectlombok:lombok:1.18.30'

    testImplementation 'org.mockito:mockito-core:4.10.0'
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.4.2'
    testImplementation 'org.junit.jupiter:junit-jupiter-params:5.4.2'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.4.2'
}

java {
    sourceCompatibility = JavaVersion.VERSION_17
    targetCompatibility = JavaVersion.VERSION_17
    withSourcesJar()
}

test {
    useJUnitPlatform()
    systemProperty 'junit.jupiter.extensions.autodetection.enabled', 'true'
}

jar {
    destinationDirectory = file("$rootDir/build/libs")
}

spotless {
    java {
        target fileTree('.') {
            include '**/*.java'
            exclude '**/build/**', '**/build-*/**'
        }
        toggleOffOn()
        ratchetFrom 'HEAD'
        googleJavaFormat()
        removeUnusedImports()
        trimTrailingWhitespace()
        endWithNewline()
    }
}

wrapper {
    gradleVersion gradle_version
}

compileJava {
    dependsOn 'spotlessApply'
}

publishing {
    repositories {
        maven {
            name = "gpr"
            url = "https://maven.pkg.github.com/I-N-T-Robotics/BubberWonker"
            credentials {
                username = System.getenv("GITHUB_ACTOR")
                password = System.getenv("GITHUB_TOKEN")
            }
        }
    }
    publications {
        maven(MavenPublication) {
            from components.java
        }
    }
}
