plugins {
    id "java"
    id "edu.wpi.first.GradleRIO" version "2023.1.1"
    id 'com.diffplug.spotless' version '6.11.0'
}

group 'com.rambots4571'

repositories {
    mavenCentral()
}

dependencies {
    implementation wpi.java.deps.wpilib()
    implementation wpi.java.vendor.java()
    testImplementation 'org.mockito:mockito-core:4.10.0'
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.4.2'
    testImplementation 'org.junit.jupiter:junit-jupiter-params:5.4.2'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.4.2'
}

sourceCompatibility = JavaVersion.VERSION_11
targetCompatibility = JavaVersion.VERSION_11

test {
    useJUnitPlatform()
    systemProperty 'junit.jupiter.extensions.autodetection.enabled', 'true'
}

jar {
    destinationDir = file("$rootDir/build/libs")
}

task sourcesJar(type: Jar) {
    from sourceSets.main.allSource
    classifier = 'sources'
}

artifacts {
    archives sourcesJar
}

tasks.register('updateGitHooks', Copy) {
    from './scripts/pre-commit'
    into './.git/hooks'
}

spotless {
    java {
        target fileTree('.') {
            include '**/*.java'
            exclude '**/build/**', '**/build-*/**'
        }
        toggleOffOn()
        ratchetFrom 'HEAD'
        googleJavaFormat()
        importOrder('com.rambots4571', 'edu.wpi')
        removeUnusedImports()
        trimTrailingWhitespace()
        endWithNewline()
    }
}

wrapper {
    gradleVersion gradle_version
}
