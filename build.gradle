buildscript {
    repositories {
        mavenCentral()
        jcenter()
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }

    dependencies {
        classpath "edu.wpi.first:GradleRIO:$wpilib_version"
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "com.jfrog.bintray.gradle:gradle-bintray-plugin:1.+"
    }
}

apply plugin: 'kotlin'
apply plugin: 'com.jfrog.bintray'
apply plugin: 'maven'
apply plugin: 'maven-publish'

group 'com.rambots4571'
version '2020.1.2'

repositories {
    mavenCentral()
    maven {
        url "https://frcmaven.wpi.edu/artifactory/release/"
    }
    maven {
        url "http://devsite.ctr-electronics.com/maven/release/"
    }
}

compileKotlin {
    kotlinOptions {
        jvmTarget = 1.8
    }
}

compileTestKotlin {
    kotlinOptions {
        jvmTarget = 1.8
    }
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
    implementation "edu.wpi.first.wpilibj:wpilibj-java:$wpilib_version"
    implementation "edu.wpi.first.wpilibNewCommands:wpilibNewCommands-java:$wpilib_version"
    implementation "edu.wpi.first.ntcore:ntcore-java:$wpilib_version"
    implementation "com.ctre.phoenix:api-java:$ctre_version"
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

jar {
    destinationDir = file("$rootDir/build/libs")
}

task sourcesJar(type: Jar) {
    from sourceSets.main.allSource
    classifier = 'sources'
}

artifacts {
    archives sourcesJar
}

publishing {
    publications {
        rampage(MavenPublication) {
            from components.kotlin
            artifact sourcesJar
            groupId "com.rambots4571"
            artifactId "rampage"
            version this.version
        }
    }
}

bintray {
    user = project.hasProperty('user') ? project.getProperty('user') : System
            .getenv("BINTRAY_USER")
    key = project.hasProperty('key') ? project.getProperty('key') : System
            .getenv("BINTRAY_KEY")
    publications = ['rampage']
    pkg {
        repo = "rampage"
        name = "rampage"
        userOrg = "rambots"
        licenses = ["MIT"]
        vcsUrl = "https://github.com/rambots/rampage"
        version {
            name = this.version
        }
    }
}

wrapper {
    gradleVersion gradle_version
}