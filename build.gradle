buildscript {
    repositories {
        mavenCentral()
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }

    dependencies {
        classpath "edu.wpi.first:GradleRIO:$wpilib_version"
        classpath "com.diffplug.spotless:spotless-plugin-gradle:$spotless_version"
    }
}

apply plugin: 'java'
apply plugin: "com.diffplug.spotless"
apply plugin: "edu.wpi.first.GradleRIO"

group 'com.rambots4571'
version '2022.1.0'

repositories {
    mavenCentral()
    maven {
        url "https://frcmaven.wpi.edu/artifactory/release/"
    }
    maven {
        url "https://maven.ctr-electronics.com/release/"
    }
}

dependencies {
    implementation wpi.java.deps.wpilib()
    implementation wpi.java.vendor.java()

    implementation "edu.wpi.first.wpilibj:wpilibj-java:$wpilib_version"
    implementation "edu.wpi.first.wpilibNewCommands:wpilibNewCommands-java:$wpilib_version"
    implementation "edu.wpi.first.ntcore:ntcore-java:$wpilib_version"
    implementation "edu.wpi.first.wpiutil:wpiutil-java:$wpilib_version"
    implementation "com.ctre.phoenix:api-java:$ctre_version"
}

sourceCompatibility = JavaVersion.VERSION_11
targetCompatibility = JavaVersion.VERSION_11

jar {
    destinationDir = file("$rootDir/build/libs")
}

task sourcesJar(type: Jar) {
    from sourceSets.main.allSource
    classifier = 'sources'
}

artifacts {
    archives sourcesJar
}

tasks.register('updateGitHooks', Copy) {
    from './scripts/pre-commit'
    into './.git/hooks'
}

tasks.register('changePermission', Exec) {
    commandLine('chmod',  '+x', './.git/hooks/pre-commit')
}

changePermission.mustRunAfter updateGitHooks

compileJava {
    dependsOn 'updateGitHooks'
    dependsOn 'spotlessApply'
}

spotless {
    java {
        target fileTree('.') {
            include '**/*.java'
            exclude '**/build/**', '**/build-*/**'
        }
        toggleOffOn()
        ratchetFrom 'HEAD'
        // eclipse().configFile('custom-code-style.xml')
        googleJavaFormat()
        importOrder('com.rambots4571', 'edu.wpi')
        removeUnusedImports()
        trimTrailingWhitespace()
        endWithNewline()
    }
}

wrapper {
    gradleVersion gradle_version
}
