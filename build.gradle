plugins {
    id "java"
    id "edu.wpi.first.GradleRIO" version "2024.2.1"
    id 'com.diffplug.spotless' version '6.25.0'
}

group 'frc.introbotics'

repositories {
    mavenCentral()
}

dependencies {
    implementation wpi.java.deps.wpilib()
    implementation wpi.java.vendor.java()

    compileOnly 'org.projectlombok:lombok:1.18.30'
	annotationProcessor 'org.projectlombok:lombok:1.18.30'

    testImplementation 'org.mockito:mockito-core:4.10.0'
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.4.2'
    testImplementation 'org.junit.jupiter:junit-jupiter-params:5.4.2'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.4.2'
}

java {
    sourceCompatibility = JavaVersion.VERSION_17
    targetCompatibility = JavaVersion.VERSION_17
}

test {
    useJUnitPlatform()
    systemProperty 'junit.jupiter.extensions.autodetection.enabled', 'true'
}

jar {
    destinationDirectory = file("$rootDir/build/libs")
}

tasks.register('sourcesJar', Jar) {
    from sourceSets.main.allSource
}

artifacts {
    archives sourcesJar
}

tasks.register('updateGitHooks', Copy) {
    from './scripts/pre-commit'
    into './.git/hooks'
}

spotless {
    java {
        target fileTree('.') {
            include '**/*.java'
            exclude '**/build/**', '**/build-*/**'
        }
        toggleOffOn()
        ratchetFrom 'HEAD'
        googleJavaFormat()
        importOrder('com.rambots4571', 'edu.wpi')
        removeUnusedImports()
        trimTrailingWhitespace()
        endWithNewline()
    }
}

wrapper {
    gradleVersion gradle_version
}

compileJava {
    dependsOn 'spotlessApply'
}
