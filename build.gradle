plugins {
    id 'org.jetbrains.kotlin.jvm' version '1.3.11' apply false
}

allprojects {
    apply plugin: 'idea'
}

subprojects {
    apply plugin: 'kotlin'
    apply plugin: 'maven'
    apply plugin: 'maven-publish'

    group 'com.rambots4571.rampage'

    buildDir = file("$rootDir/build/${project.name}")

    repositories {
        mavenCentral()
        maven {
            url "http://first.wpi.edu/FRC/roborio/maven/release/"
        }
        maven  {
            url "http://devsite.ctr-electronics.com/maven/release/"
        }
    }

    jar {
        destinationDir = file("$rootDir/build/libs")
    }

    dependencies {
        implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
    }

    sourceCompatibility = java_version

    compileKotlin {
        kotlinOptions.jvmTarget = java_version
    }

    compileTestKotlin {
        kotlinOptions.jvmTarget = java_version
    }

    task sourcesJar(type: Jar) {
        from sourceSets.main.allSource
        classifier = 'sources'
        destinationDir = file("$rootDir/build/libs")
    }

    artifacts {
        archives sourcesJar
    }
}

wrapper {
    gradleVersion gradle_version
}